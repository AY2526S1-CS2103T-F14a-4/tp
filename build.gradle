plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'application'
    id 'jacoco'
}

application {
    mainClass = 'seedu.address.Main'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = '10.17.0'
    configFile = file("config/checkstyle/checkstyle.xml")
}

jacoco {
    toolVersion = "0.8.8"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }
}

dependencies {
    def jUnitVersion = '5.4.0'
    def javaFxVersion = '17.0.7'
    def platforms = ['win', 'mac-aarch64', 'linux']

    platforms.each { platform ->
        implementation "org.openjfx:javafx-base:${javaFxVersion}:${platform}"
        implementation "org.openjfx:javafx-controls:${javaFxVersion}:${platform}"
        implementation "org.openjfx:javafx-fxml:${javaFxVersion}:${platform}"
        implementation "org.openjfx:javafx-graphics:${javaFxVersion}:${platform}"
    }

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.7.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.7.4'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${jUnitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jUnitVersion}"
}

shadowJar {
    archiveFileName = 'addressbook.jar'
}

defaultTasks 'clean', 'test', 'jacocoTestReport'
